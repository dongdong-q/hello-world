.text
.global _start

_start:

	/* 关闭看门狗 */
	ldr r0, =0x53000000
	ldr r1, =0
	str r1, [r0]

	/* 设置内存: sp 栈 */
	/* 分辨是nor/nand启动
	 * 写0到0地址, 再读出来
	 * 如果得到0, 表示0地址上的内容被修改了, 它对应ram, 这就是nand启动
	 * 否则就是nor启动
	 */
	mov r1, #0
	ldr r0, [r1] /* 读出原来的值备份 */
	str r1, [r1] /* 0->[0] */ 
	ldr r2, [r1] /* r2=[0] */
	cmp r1, r2   /* r1==r2? 如果相等表示是NAND启动 */
	ldr sp, =0x40000000+4096 /* 先假设是nor启动 */
	moveq sp, #4096  /* nand启动 */
	streq r0, [r1]   /* 恢复原来的值 */

	/* OM[2:3]=0b00 */
	/* LOCKTIME DEFAULT VALUE *(0X4C000000)=0xFFFFFFFF */
	ldr r0,=0xFFFFFFFF
	ldr r1,=0x4C000000
	str r0,[r1]


	/* MPLLCON 400MHZ *(0x4C000004)=0x0005c011 */
	/* MDIV=92 PDIV=1 SDIV=1 MPLL=400MHZ */
	/* 修改48MHZ 0x00038022 */
	ldr r0,=0x0005c011
	ldr r1,=0x4C000004
	str r0,[r1]

	/* 设置cpu异步模式和fast bus */
	mrc p15,0,r0,c1,c0,0 
 	orr r0,r0,#0xc0000000 /* #R1_nF:OR:R1_iA */ 
	mcr p15,0,r0,c1,c0,0

	/* 设置PLL for FCLK PCLK HCLK */
	/* 设置 CLKDIVN *(0x4C000014)=0x5 */
	/* HCLK=FCLK/4 PCLK=HCLK/2 */
	ldr r0,=0x5
	ldr r1,=0x4C000014
	str r0,[r1]

	/* MPLLCON 400MHZ *(0x4C000004)=0x0005c011 */
	/* MDIV=92 PDIV=1 SDIV=1 MPLL=400MHZ */
	/* 修改48MHZ 0x00038022 */
	ldr r1,=0x4C000004
	ldr r2,[r1]
	ldr r3,=0x0005c011
	mov r0,#1
	cmp r3,r2
	moveq r0,#0
	str r3,[r1]


	bl main

halt:
	b halt
	


